<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.24">
  <POU Name="FB_Logger" Id="{f01e87a0-60ff-4302-b8a9-762e186f392e}">
    <Declaration><![CDATA[FUNCTION_BLOCK PUBLIC FB_Logger
VAR
	pTxtLogger   : POINTER TO FB_TxtLogger;
	pCsvLogger   : POINTER TO FB_CsvLogger;
	pXmlLogger   : POINTER TO FB_XmlLogger;
	ipLogger     : ILogger;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[ ]]></ST>
    </Implementation>
    <Method Name="Write" Id="{34777f0c-26fa-45cc-9bc6-6bb5d033ffcd}">
      <Declaration><![CDATA[METHOD PUBLIC Write : STRING
VAR_INPUT
	sMsg    : STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (ipLogger <> 0) THEN
	Write := ipLogger.Write(CONCAT('logger: ', sMsg));
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{91f81cdc-db00-470f-9267-c46b6b4c20e0}">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (pTxtLogger <> 0) THEN
	__DELETE(pTxtLogger);
	pTxtLogger := 0;
END_IF

IF (pCsvLogger <> 0) THEN
	__DELETE(pCsvLogger);
	pCsvLogger := 0;
END_IF

IF (pXmlLogger <> 0) THEN
	__DELETE(pXmlLogger);
	pXmlLogger := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{bf5e1ee7-39b3-4fbd-a285-cfa365e86670}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode  : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    sFilename    : STRING;
END_VAR
VAR
	sFilenameExtension : STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[sFilenameExtension := F_ToLCase(RIGHT(sFilename, 3));
IF (sFilenameExtension = 'txt') THEN
    pTxtLogger := __NEW(FB_TxtLogger);
    ipLogger := pTxtLogger^;
ELSIF (sFilenameExtension = 'csv') THEN
    pCsvLogger := __NEW(FB_CsvLogger);
    ipLogger := pCsvLogger^;
ELSIF (sFilenameExtension = 'xml') THEN
    pXmlLogger := __NEW(FB_XmlLogger);
    ipLogger := pXmlLogger^;
ELSE
    ipLogger := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <ObjectProperties />
  </POU>
</TcPlcObject>